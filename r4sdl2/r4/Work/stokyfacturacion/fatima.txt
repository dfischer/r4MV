| FATIMA
| PHREDA 2013
|-------------------------
^r4/lib/gui.txt
^r4/lib/input.txt
^r4/lib/db2.txt
^r4/lib/grid.txt
^r4/lib/clock.txt

^r4/lib/ricons.txt
^inc/ric/fontawesomewebfont.ric

|^r4/lib/rfont.txt
|^inc/rft/robotobold.rft

|-------------------------------------------
| db stock

#dbsID			)( 16
#dbsNombre  	)( 64
#dbsDetalle 	)( 8192 | detalle= talle/color| cantidad
#dbsCantidad	)( 16
#dbsMarca		)( 32
#dbsCategoria	)( 32
#dbsCosto 		)( 16
#dbsVenta		)( 16

#dbStock 0 "db/fatima/stock.db"
#dbStocko dbsID dbsNombre dbsCantidad dbsMarca dbsCategoria dbsCosto dbsVenta dbsDetalle 0
#grStock 0 0
$200 $501 $303 $10102 $40206 0
"Código|Nombre|Marca|Cnt|Venta"

:cdiferencia
	?real2 swap >fld> ?real2 nip | costo venta
	over - 10000 rot */
	nro2str gcellr ;

#grStockp 0 0
$200 $501 $40205 $40206 $60205 0
"ID|Nombre|Costo|Venta|Diferencia"
'cdiferencia

|-------------------------------------------
| db venta

#dbvID			)( 16
#dbvCliente		)( 64
#dbvfecha		)( 16
#dbvarticulo	)( 1024
#dbvcondicion	)( 4
#dbvTotal		)( 16
#dbvEntrego		)( 16

#dbVenta 0 "db/fatima/ventas.db"
#dbVentao dbvID dbvCliente dbvfecha dbvarticulo dbvcondicion dbvtotal dbventrego 0
#grVenta 0 0
$10202 $401 $10104 $40306 0
"Fecha|Cliente|Modo|Entrego"

|-------------------------------------------
| linea de venta

#dbvlfecha	)( 16
#dbvlcod	)( 16
#dbvlnombre	)( 128
#dbvlcoltal	)( 32
#dbvlprecio	)( 16
#dbvlcond	)( 4
#dbvlpago	)( 16

#dbVenLin 0 "db/fatina/venlin.db"
#dbVenLino dbvlfecha dbvlcod dbvlnombre dbvlcoltal dbvlprecio dbvlcond dbvlpago 0
#grVenLin 0 0
$401 $402 $403 $404 $405 $406 0
"Codigo|Nombre|Color/Talle|Precio|Condicion|Entrega"

|-------------------------------------------
| db lventa
#dblfecha		)( 16
#dbldetalle		)( 128
#dblcondicion	)( 4
#dblTotal		)( 16

#dbVental 0 "db/fatima/lventas.db"
#dbVentalo dblfecha dbldetalle dblcondicion dblTotal 0
#grVental 0 0
$10200 $401 $10102 $40303 0
"Fecha|Detalle|Condicion|Total"


|-------------------------------------------
| db cambio
#dbcID		)( 16
#dbcCliente	)( 32
#dbcFecha	)( 16
#dbcART1    )( 16
#dbcART2	)( 16
#dbentrego	)( 16

#dbCambio 0 "db/fatima/cambios.db"
#dbCambioo dbcID dbcCliente dbcFecha dbcART1 dbcART2 dbentrego 0
#grCambio 0 0
$10202 $401 $10203 $10204 $40305 0
"Fecha|Cliente|Dejo|Llevo|Entrego"

|-------------------------------------------
#pad )( 64

#talcol
#canttc
#memtallecolor )( 4096

#grListaTalle 0 0
$1f00 $801 0
"Color/Talle|Cantidad"

:clicklistatalle
	;

:inputstock
	780 440 dim 0 dup fpos
	negro gc.fbox home
	azul linefill
	blanco "Ficha de Articulo" printc cr cr2
	" Código: " print
	12 col 'dbsID 15 input cr
	" Nombre: " print
	12 col 'dbsNombre 63 input cr

	" Marca: " print
	12 col 'dbsMarca 31 input
	40 col " Categoria: " print
	52 col 'dbsCategoria 31 input cr

	" Costo: " print
	12 col 'dbsCosto 12 2 inputreal
	40 col " Venta: " print
	52 col 'dbsVenta 12 2 inputreal cr

|	" Cantidad: " print
|	14 col 'dbsCantidad 15 input cr

	16 'tx1 +! -16 'tx2 +!

	8 'memtallecolor 'grlistatalle editgrid

	-16 'tx1 +! 16 'tx2 +!
	cr
	azul 3 linesfill cr
	;


:resetStock
	0 'pad !
	cleardbgrid
	-1 'grStock !

	0 'dbsID !
	0 'dbsNombre !
	0 'dbsDetalle !
	0 'dbsCantidad !
	0 'dbsMarca !
	0 'dbsCategoria !
	0 'dbsCosto !
	0 'dbsVenta !
	;

|-------------------------------------------
:titulo
	clrscr
|	64 gc.top negro gc.fbox
|	gc.all $ff0000 $ff hfil
|	scr
	0.09 0.1 fdim
	0.9 0.88 fpos almanaque
	fonta-Arial14 scr blanco
	cr2
	"           Fátima " print
|	pick2 pick2 pick2 "%d %d %d" print
	cr
	;

|-------------------------------------------
#detalleventa )( $fff

#padart )( 32
#codart )( 16
#cntart )( 8
#stotal )( 16
#total
#spagado )( 16
#pagado

#grlistaven 0 0
$400 $701 $10202 $40203 $40204 0
"Codigo|Descripción|Cantidad|Precio|Total"

:resetventa
	0 'dbvID !
	0 'dbvCliente !
	0 'dbvfecha !
	0 'dbvarticulo !
	0 'dbvcondicion !
	0 'dbvTotal !
	0 'dbvEntrego !
	;

|----------------------
:recalc
	0 'stotal !
	0 'total !
	'detalleventa
	( >fld> 1? )( >fld> >fld> >fld> ?real2 'total +! >fld> ) drop
	total nro2str 'stotal strcpy
	'stotal 'spagado strcpy
	;

:deleteline | nro --
	5 * 'detalleventa swap >>fld
	dup >fld> >fld> >fld> >fld> >fld> | rdel rnex
	0? ( 2drop ; )
	swap strcpy ;

:delline
	grlistaven -1 =? ( drop ; )
	deleteline
	recalc ;

:clicklista
	;

| copiar de grilla memoria a db
:dbcpy | adrs adrd --
	mark 'here !
	( c@+ 32 =? )( drop )
	0? ( ,c drop empty ; ) drop
	1-
	( c@+ 1? )(
		$25 =? ( 32 nip ) | % -> %%
		$7c =? ( $5f nip ) | cambia separadores
		$7e =? ( 32 nip ) | separadores
		,c ) drop 2 -
	( dup c@ 32 =? )( drop 1- -1 'here +! ) 2drop
	0 ,c
	empty ;

| copiar de grilla db a memoria
:dbcpyi | adrs adrd --
	mark 'here !
	( c@+ 32 =? )( drop )
	0? ( ,c drop empty ; ) drop
	1-
	( c@+ 1? )(
		$7e =? ( $7c nip )
		$7c =? ( 2drop 0 ,c empty ; )
		$5f =? ( $7c nip ) | cambia separadores
		,c ) drop 2 -
	( dup c@ 32 =? )( drop 1- -1 'here +! ) 2drop
	0 ,c
	empty ;

|--- actualiza stock

#tcod )( 32

:artstock | cant "cod" --
	0 'dbstock dbsearchstr 0? ( 2drop ; ) @ 'tcod !
	tcod 'dbStocko dbDeSerial

	neg 'dbsCantidad str>int + "%d" mprint 'dbsCantidad strcpy

	'dbStocko dbSerial
	tcod 'dbStock dbupdatemlog
	;

:actualizastock
	'detalleventa
	(	dup "%a" mprint 'tcod strcpy
		>fld> >fld> 0? ( drop ; )
		dup "%a" mprint str>int
		'tcod artstock
		>fld> >fld> >fld> 0? )
	drop ;

|--- grabar venta

:exitventa
	'dbventa dbcntregs "%d" mprint 'dbvID strcpy
	'stotal 'dbvtotal strcpy
	'spagado 'dbventrego strcpy
	'detalleventa 'dbvarticulo dbcpy
	'dbVentao dbSerial
	'dbVenta dbinsertlog
	actualizastock
	exit ;

:ventaef
	"1" 'dbvcondicion strcpy
	exitventa ;
:ventach
	"2" 'dbvcondicion strcpy
	exitventa ;
:ventade
	"3" 'dbvcondicion strcpy
	exitventa ;
:ventacr
	"4" 'dbvcondicion strcpy
	exitventa ;

:agregaotro
	0 'codart !
	"1" 'cntart strcpy
	1 setfoco | para ingresar otro
	;

:addline
	'codart 0 'dbStock dbsearchstr
	0? ( drop ; ) | no existe codigo
	'cntart ?sint nip 0? ( 2drop ; ) drop | no hay cantidad

	@
	dup "%a|" mprint 'detalleventa strcat		| codigo
	>fld> dup "%a|" mprint 'detalleventa strcat | descripcion
	'cntart "%s|" mprint 'detalleventa strcat	| cantidad
	>fld> >fld> >fld> >fld> >fld>
	dup "%a|" mprint 'detalleventa strcat		| venta
	?real2 'cntart ?sint nip *
	nro2str 'detalleventa strcat
	"|" 'detalleventa strcat
	drop

	agregaotro
	recalc ;

:ventaviejo
	-1 'grlistaven !
	0 'detalleventa !
	0 'codart !
	0 'cntart !

	0 'spagado !
	0 'pagado !
	0 'stotal !

	resetventa

	date "%d/%d/%d" mprint 'dbvfecha strcpy
	agregaotro
	show titulo
		"           > Inicio > Venta" print

		820 468 dim 0 dup fpos
		negro gc.fbox home
		azul 1 linesfill
		blanco "Venta" printc cr cr2

		" Código:" print 'codart 16 sp input
		" Cantidad:" print 'cntart 8 sp inputint
		verde
		'addline " Agrega " sp .link
		'delline " Quita " sp .link
		cr blanco
		cr2
		grlistaven
		10 'detalleventa 'grlistaven memgrids
		grlistaven <>? ( clicklista ) drop
		cr blanco
		" Cliente:" print 'dbvCliente 48 sp input cr

		" Total:   " print 14 col "$ " print 'stotal print cr
		" Entrego: " print 14 col "$ " print 'spagado 16 input cr
		cr
|		cyan 'pagatodo "Aceptar Total" sp .btnt
		verde 'ventaef "Efectivo" sp .btnt
		verde 'ventach "Cheque" sp .btnt
		verde 'ventade "Débito" sp .btnt
		verde 'ventacr "Crédito" sp .btnt
		sp sp
		rojo 'exit "Cancelar" sp .btnt

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric
		cminiflecha ;

|---- venta con lineas
:obtienearticulo
	'dbvlcod 0 'dbStock dbsearchstr
	0? ( drop ; ) | no existe codigo
	@
	>fld> dup "%a" mprint 'dbvlnombre strcpy
	>fld> >fld> >fld> >fld> >fld>
	drop

|	dup "%a|" mprint 'detalleventa strcat		| venta
|	?real2 'cntart ?sint nip *
|	nro2str 'detalleventa strcat

	;

:addlinea
	;


:venta

	0 'dbvlfecha !
	0 'dbvlcod !
	0 'dbvlnombre !
	0 'dbvlcoltal !
	0 'dbvlprecio !
	0 'dbvlcond !
	0 'dbvlpago !

	date "%d/%d/%d" mprint 'dbvlfecha strcpy

	show titulo
		"           > Inicio > Venta" print

		820 468 dim 0 dup fpos
		negro gc.fbox home
		azul 1 linesfill
		blanco "Venta" printc cr cr2

		" Código:" print 'dbvlcod 16 sp input
		'obtienearticulo lostfoco
		verde
		cr blanco
		cr2
		'dbvlnombre print



|		grlistaven
|		10 'detalleventa 'grlistaven memgrids
|		grlistaven <>? ( clicklista ) drop

		cr blanco
|		" Cliente:" print 'dbvCliente 48 sp input cr

		" Total:   " print 14 col "$ " print 'stotal print cr
		" Entrego: " print 14 col "$ " print 'spagado 16 input cr
		cr
|		cyan 'pagatodo "Aceptar Total" sp .btnt
		verde 'ventaef "Efectivo" sp .btnt
		verde 'ventach "Cheque" sp .btnt
		verde 'ventade "Débito" sp .btnt
		verde 'ventacr "Crédito" sp .btnt
		sp sp
		rojo 'exit "Cancelar" sp .btnt


		grVental
		17 'grVenLin 'dbVenLin dbgrid
		|grVenLin swap <>? (  )
		drop


		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric
		cminiflecha ;



|-------------------------------------------
#codart1 )( 64
#precio1
#codart2 )( 64
#precio2

:buscoart
	'dbcART1 0 'dbStock dbsearchstr
	1? ( @ >fld> dup >fld> >fld> >fld> >fld> >fld> ?real2 nip 'precio1 ! "%a" mprint )( "" nip )
	'codart1 strcpy

	'dbcART2 0 'dbStock dbsearchstr
	1? ( @ >fld> dup >fld> >fld> >fld> >fld> >fld> ?real2 nip 'precio2 ! "%a" mprint )( "" nip )
	'codart2 strcpy
	;

:guardocambio
	precio1 0? ( drop ; ) drop
	precio2 0? ( drop ; ) drop

	'dbCambio dbcntregs "%d" mprint 'dbcID strcpy
	"" 'dbcCliente strcpy
	date "%d/%d/%d" mprint 'dbcFecha strcpy
|		'dbentrego
	'dbCambioo dbSerial
	'dbCambio dbinsertlog

	-1 'dbcART1 artstock | cant "cod" --
	1 'dbcART2 artstock | cant "cod" --

	exit
	;

:cambio
	0 'dbcART1 ! 0 'codart1 ! 0 'precio1 !
	0 'dbcART2 ! 0 'codart2 ! 0 'precio2 !
	0 'dbentrego !
	show titulo
		"           > Inicio > Cambio" print

		800 468 dim 0 dup fpos
		negro gc.fbox home
		azul 1 linesfill
		blanco "Cambio de Articulos" printc cr cr2


		" Dejo:" print 'dbcART1 16 sp input
		'buscoart lostfoco
		20 col 'codart1 print
		60 col "$ " print precio1 nro2str print cr
		" Llevo:" print 'dbcART2 16 sp input
		'buscoart lostfoco
		20 col 'codart2 print
		60 col "$ " print precio2 nro2str print cr
		30 col " Diferencia: " print
		60 col "$ " print precio2 precio1 - nro2str print cr
		30 col " Entrego: " print
        60 col "$ " print
		'dbentrego 16 input
		cr cr

		verde
		'guardocambio " Cambio " sp .link
		'exit " Cancela " sp .link cr cr
		blanco " Cambios Anteriores" print

		grCambio
		10 'grCambio 'dbCambio dbgrid
		grCambio <>? ( clicklista ) drop

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric
		cminiflecha ;

|-------------------------------------------

:entrecc
	;

|-------------------------------------------
:updStock
	'dbStocko dbSerial dup 'dbStock dbupdatelog exit ;

:modiStock | nro -- nro
	dup 'dbStock dbgetreg 'dbStocko dbDeSerial
	show titulo
		"           > Inicio > Stock > Editar" print
		cr cr chome!

		inputstock
		verde 'updStock " Aceptar " sp .btnt
		rojo 'exit " Cancelar " sp .btnt

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha ;

:insStock
	'dbStocko dbSerial 'dbStock dbinsertlog exit ;

:altaStock
	resetStock
	show titulo
		"           > Inicio > Stock > Nuevo" print
		cr cr chome!

		inputstock
		verde 'insStock " Aceptar " sp .btnt
		rojo 'exit " Cancelar " sp .btnt

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha ;

:testart | reg -- reg 1/0
	'pad over reg?i ;

:filterstk
	'pad c@ 1? ( 'testart nip ) 'dbStock dbfilterv
	0 'grStock 4+ !
	;

:stock
	0 'dbstock dbfilterv
	resetstock
	0 'pad !
	show titulo
		"           > Inicio > Stock " print
		800 468 dim 0 dup fpos
		negro gc.fbox home
		azul 3 linesfill
		blanco "Stock" printc cr

		verde
		'altastock " Nuevo " sp btnt sp

		cyan " Filtro: " print
		'filterstk 'pad 63 inputexec

|		grStockp
|		17 'grStockp 'dbStock dbgrid
|		grStockp swap <>? ( modiStock cleardbgrid ) drop

		grStock
		17 'grStock 'dbStock dbgrid
		grStock swap <>? ( modiStock cleardbgrid ) drop

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha ;

|----------------- reportes

#grventasdia 0 0
$30200 $40201 $40202 $40203 $40204 $40205 $40206 $40207 $30108 0
"Dia|Efectivo|CC|Debito|Credito|Ing.CC|Deb.CC|Total|Art."

#nano
#nmes
#smes
|#nday
#diahoy
#vtasef
#vtascc
#vtasdeb
#vtascre
#ingcc
#ingccdeb
#vtastotal
#arttotal
#diastotal
#grillaesta )( $ffff

:nowday
	date drop 'nmes ! 'nano ! ;

#meses "Enero" "Febrero" "Marzo" "Abril" "Mayo" "Junio" "Julio" "Agosto" "Setiembre" "Octubre" "Noviembre" "Diciembre"

:getmes
	'meses nmes ( 1? )( 1- swap >>0 swap ) drop
	'smes ! ;

:resetestadia
	0 'vtasef !
	0 'vtascc !
	0 'vtasdeb !
	0 'vtascre !
	0 'ingcc !
	0 'ingccdeb !
	0 'detalleventa !
	;

#fechai
#fechaf

|#dbVentao dbvID dbvCliente dbvfecha dbvarticulo dbvcondicion dbvtotal dbventrego 0

:cuentastock | -- cnt
	0 'detalleventa
	( >fld> >fld>
		dup "%a" mprint str>int rot + swap
		>fld> >fld> >fld> 0? )
	drop ;

:acumula
	dup 4 >>fld str>int
	over 6 >>fld str>int
	dup rot 1- 2 << 'vtasef + +!
	'vtastotal +!
	dup 3 >>fld 'detalleventa dbcpyi
	cuentastock 'arttotal +!
	;

:gengrilla
	diahoy 0? ( drop ; )
	date>str 'grillaesta strcat
	arttotal vtastotal ingccdeb ingcc vtascre vtasdeb vtascc vtasef
	"|%d|%d|%d|%d|%d|%d|%d|%d|" mprint 'grillaesta strcat
	1 'diastotal +!
	resetestadia ;

:sumstat | row --
	dup 2 >>fld str>date
	fechai <? ( 2drop ; )
	fechaf >? ( 2drop ; )
	diahoy =? ( drop acumula drop ; )
	gengrilla
	'diahoy !
	acumula drop
	;

:calcstadia
	0 'vtastotal !
	0 'arttotal !
	0 'diahoy !
	0 'diastotal !
	0 'grillaesta !

	nano nmes 1 "%d/%d/%d" mprint str>date 'fechai !
	nano nmes 31 "%d/%d/%d" mprint str>date 'fechaf !

	2 'dbventa dbsortdate
	resetestadia
	'sumstat 'dbventa dbmapi
	gengrilla
	;

:prevday
	nmes 1- 0? ( 12 nip -1 'nano +! ) 'nmes !
	getmes calcstadia ;

:nextday
	nmes 1+ 13 =? ( 1 nip 1 'nano +! ) 'nmes !
	getmes calcstadia ;


:ventad
	nowday getmes
	calcstadia
	show titulo

		860 468 dim 0 dup fpos
		negro gc.fbox home
		azul 1 linesfill

		cyan
		sp 'prevday "<< mes" link
		sp 'nextday "mes >>" link cr cr2
		blanco nano smes " %s de %d " print
		cr
		grlistaven
		10 'grillaesta 'grventasdia memgrids
		grlistaven <>? ( clicklista )
		drop

		blanco
		cr
		" Total Ventas: " print vtastotal "%d" mprint memnro2 print cr
		" Promedio Mensual: " print vtastotal diastotal 0? ( 1 nip ) / "%d" mprint memnro2 print cr
		cr
		" Total Articulos: " print cr

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha ;

|-------------------------------------------
:diario
	nowday getmes
	calcstadia

	show titulo
		"           > Inicio > Reportes" print

		600 420 dim 0 0 fpos
		negro gc.fbox home
		azul 2 linesfill
		blanco "Diario" cr2 printc cr

		grVental
		17 'grVental 'dbVental dbgrid
		grVental swap <>? ( modiStock cleardbgrid )
		drop
		blanco
		" Total Ventas: " print vtastotal "%d" mprint memnro2 print cr
		" Promedio Mensual: " print vtastotal diastotal 0? ( 1 nip ) / "%d" mprint memnro2 print cr
		cr
		" Total Articulos: " print cr


		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha ;

|-------------------------------------------

:reportes
	show titulo
		"           > Inicio > Reportes" print

		600 320 dim 0 0 fpos
		negro gc.fbox home
		azul 2 linesfill

		blanco "Reportes" cr2 printc cr cr cr2
		verde
		'ventad dup <f1> "F1 - Ventas Diarias" sp sp sp 18 btnm cr cr

		rojo
		'exit "ESC - Salir" sp 18 btnm cr cr

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha ;

:estadisticas
	show titulo
		"           > Inicio > Estadistica" print

		600 320 dim 0 0 fpos
		negro gc.fbox home
		azul 2 linesfill

		blanco "Estadisticas" cr2 printc cr cr cr2
		verde
		rojo
		'exit "ESC - Salir" sp 18 btnm cr cr

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha ;


|-------------------------------------------
:main
	33
	show titulo
		"           > Inicio " print
		dup "%d" print

		600 370 dim 0 0 fpos
		negro gc.fbox home
		azul 2 linesfill

		blanco "Menu Principal" cr2 printc cr cr cr2
		verde
		'venta dup <f1> "F1 - Venta" sp sp sp 18 btnm cr cr
		'cambio dup <f2> "F2 - Cambio" sp sp sp 18 btnm cr cr
		'entrecc dup <f3> "F3 - Entrega CC" sp sp sp 18 btnm cr cr

		'diario dup <f4> "F4 - Diario" sp 18 btnm cr cr
		'stock dup <f5> "F5 - Stock" sp 18 btnm cr cr
		'reportes dup <f6> "F6 - Reportes" sp 18 btnm cr cr
		'estadisticas dup <f7> "F7 - Estadistica" sp 18 btnm cr cr cr2
		rojo
|		'exit "ESC - Salir" sp 18 btnm cr cr

		48 qdim 32 32 pos
		rojo 'exit dup >esc< btne 40 qdim blanco 'i.off drawric

		cminiflecha
		;

:inicio
	mark
	$7f7f7f paper
	'dbStock dbload
	'dbVenta dbload
	'dbCambio dbload
	'dbVental dbload

	'dbVenLin dbload

|	"1|2|" 'memtallecolor strcpy
	;

: inicio main  ;
