| gui - pabloreda@gmail.com
| PHReda
|----------
^reda4.txt
^fuente8.txt

#x1 #y1 #x2 #y2 | zona de dibujo
#xx -1 #yy -1 
#nw #nh	| aux y tabla
#xa #ya #xb #yb | zona actual
#:guiact 0
#activo 0
#teclados )( 8192 | guarda teclados
#teclados> 'teclados
#zns	)( 8192 | pila de zonas
#zns>	'zns	
#za>	'zns	
#zf>	'zns
#dlg	)( 256
#dlg>	'dlg

|::.cls
|	'zns dup dup 'zns> ! 'za> ! 'zf> ! 'dlg 'dlg> ! -1 dup 'xx ! 'yy ! ;

#lastline

::.table | cf -- 
	sh swap / 'nh ! sw swap / 'nw ! -1 dup 'xx ! 'yy ! 0 0
::.at | xy --
	nh * dup nh + 'y2 ! 'y1 ! nw * dup nw + 'x2 ! 'x1 ! ;
::.to | xy --
	nh * y1 + 'y2 ! nw * x1 + 'x2 ! ;
::.> 	
	x1 nw + sw >=? ( 0 nip 'x1 ! nw 'x2 ! y2 y1 - dup 'y1 +! 'y2 +! ; ) 'x1 ! 
	nw 'x2 +!	; 		| x2 x1 - dup 'x1 +! 'x2 +! 
::.<
	x1 nw - 0 <? ( 0 nip 'x1 ! nw 'x2 ! y2 y1 - neg dup 'y1 +! 'y2 +! ; ) 'x1 !
	nw neg 'x2 +! ;
::.\
	y1 nh + sh >=? ( 0 nip 'y1 ! nh 'y2 ! x2 x1 - dup 'x1 +! 'x2 +! ; ) 'y1 ! 
	nh 'y2 +! ;
::./ 
	y2 y1 - neg dup 'y1 +! 'y2 +! ;
::all
	x2 x1 - y2 y1 - dim
::center	
	x1 x2 + 2/ y1 y2 + 2/ pos ;
	
::rect.
	y2 x2 y1 x1 ;
	
|--------pantalla
:inbox? | x y -- 1/0
	y1 <? ( 2drop 0 ; ) y2 >? ( 2drop 0 ; ) drop
	x1 <? ( 0 nip ; ) x2 >? ( 0 )( 1 ) nip ;

:inboxb? | x y -- 1/0
	ya <? ( 2drop 0 ; ) yb >? ( 2drop 0 ; ) drop
	xa <? ( 0 nip ; ) xb >? ( 0 )( 1 ) nip ;

|---- condicion,var,key
:ex00 drop ;
:ex01 over @ @ over 4 >> $fff and <>? ( drop 16 >> 'activo ! )( 2drop ) 4+ ;
:ex10 drop @+ @+ 'x1 ! @+ 'y1 ! @+ 'x2 ! @ 'y2 ! ; 	| variables por programa	
:ex11 drop ;
	
#extras 'ex00 'ex01 'ex10 'ex11

:p0 dup $c and 'extras + @ exec ; 
:p1 d>xy 'x2 ! 'x1 ! ; 
:p2 d>xy 'y2 ! 'y1 ! ; 
|---- Dibujando
:p3
	2 >> dup 6 >> ink	$3f and 
	activo 1? ( 1- 'activo ! + ; ) drop
	y2 x2 y1 x1 >win
	>r dup @ exec r> + ; 

#toc 0	
|---- Buscando	
:b3
	2 >> $3f and 
	activo 1? ( 1- 'activo ! + ; ) drop
	2over inbox? 1? ( y2 x2 y1 x1 'xa !+ !+ !+ ! pick2 'toc ! ) drop 
	+ ;

#pcom p0 p1 p2 p3
:dlg.draw | --
	0 'activo !
	za> ( @+ 1? )( dup $3 and 2 << 'pcom + @ exec ) 2drop ;
	
#bbus p0 p1 p2 b3
:dlg.busca | xy -- xy0/addr
	0 'activo ! 0 'toc ! 
	za> ( @+ 1? )( dup $3 and 2 << 'bbus + @ exec ) 2drop toc ;
	
|--- interaccion
:msinicio | xy --
	dlg.busca 0? ( 'guiact ! 2drop ; ) dup 'guiact ! 2
:mscal
	swap 4+ @ 0? ( 4drop ; ) yb xb ya xa 
	2over 2over >win | sacar esto
	'x1 !+ !+ !+ !
	exec ;
:msup
	0 motion! 0 end! guiact 0? ( drop ; ) 
	>r xymouse r> 0 mscal 0 'guiact ! ;
:msmove
	xymouse guiact 0? ( drop msinicio ; ) >r
	2dup inboxb? 0? ( rdrop drop msinicio ; ) drop r>
	1 mscal ;
:msdown
	'msmove motion! 'msup end! xymouse msinicio ;


|---- compila dialogo
:,. | o --
	zns> !+ 'zns> ! ;
	
:savelugar
	x1 x2 xy>d xx <>? ( dup 'xx ! 1 or ,. )( drop )
	y1 y2 xy>d yy <>? ( dup 'yy ! 2 or ,. )( drop ) ;
:col3 | cnt -- col
	$3f and 2 << ink@ 8 << or 3 or ;
	
::.ug	| cd --
	savelugar 8 col3 ,. ,. ,. ;
::.ug1 | acd -- 
	savelugar 12 col3 ,. ,. ,. ,. ;
::.ug2 | bacd --	
	savelugar 16 col3 ,. ,. ,. ,. ,. ;
::.ug3 | cbacd --	
	savelugar 20 col3 ,. ,. ,. ,. ,. ,. ;

::.cond | vnc -- | variable valor cantidad
	16 << swap 4 << or $4 or ,. ,. ;

::.pos | 'd -- variables de posicion (4 enteros)
	x1 x2 xy>d 'xx ! y1 y2 xy>d 'yy !
	$8 ,. ,. ;

::*?   guiact =? ( 1 )( 0 ) ;
::var1	8 + @ ;
::var2	12 + @ ;
::var3	16 + @ ;

|--- botones spoli,text
:cboton | xyv --
	0? ( 3drop guiact var1 exec ; ) 3drop ;
	
:dboton
	guiact =? ( claro -4 )( -2 ) dup +dim rcaja  	
	rcaja blanco dup var2 rpoli ;
	
::.sbtn | 'sprite 'click --
	'cboton 'dboton .ug2 ;

:dbotont
	guiact =? ( claro -4 )( -2 ) dup +dim rcaja  
	blanco 6 1 ttext dup var2 pcenter print ;
	
::.tbtn | "hola" 'click --	
	'cboton 'dbotont .ug2 ;

:8botont
	guiact =? ( claro -4 )( -2 ) dup +dim rcaja  
	blanco 12 1 ttext dup var2 pcenter print ;

::.8btn | "hola" 'click --	
	'cboton '8botont .ug2 ;

:4botont
	guiact =? ( claro -4 )( -2 ) dup +dim rcaja  
	blanco 3 1 ttext dup var2 pcenter print ;

::.4btn | "hola" 'click --	
	'cboton '4botont .ug2 ;

:2botont
	guiact =? ( claro -4 )( -2 ) dup +dim rcaja  
	blanco 2 1 ttext dup var2 pcenter print ;

::.2btn | "hola" 'click --	
	'cboton '2botont .ug2 ;

:dib.gboton
	ink@ swap guiact =? ( verde fill ) swap
	ink 12 1 ttext dup var2 pcenter print ;

::.gbtn | "hola" 'click --	
	'cboton 'dib.gboton .ug2 ;

::.xbtn | 'vec 'click --
	'cboton rot .ug1 ;

:funbtn
	guiact =? ( claro -4 )( -2 ) dup +dim rcaja  
	blanco 5 1 ttext dup var2 pcenter print 
	negro 8 2 ttext dup var3 "  %s" print	;
	
::.fbtn	| "F1" "hola" 'click --
	'cboton 'funbtn .ug3 ;
	
|---sliders
:x>scl | max xr -- x'
	x1 max x2 min x1 - * x2 x1 - / ;
:scl>x | max x' -- xr
	0 max over min x2 x1 - * swap / x1 + ;
:y>scl | max yr -- x'
	y1 max y2 min y1 - * y2 y1 - / ;
:scl>y
	0 max over min y2 y1 - * swap / y1 + ;

:chsli | xyv --
	0? ( 3drop ; ) 2drop 
	guiact dup >r var2 swap x>scl r> var1 ! ;

:riellh
	ink@ y1 y2 + 2/ dup dup blanco 1- vlinea gris vlinea 1+ negro vlinea ink ;
	
:dhsli
	riellh dup var2 over var1 @ scl>x 
	x1 max x2 min dup y2 y1 + 2/ y2 y1 - 2/ 2/ dup >r  fcircle r>
	2/ dup >r - y2 y1 + 2/ r - r> 2/ blanco fcircle ;
	
::.hsli | max var --  | slide de 0..max
	'chsli 'dhsli .ug2 ;

:cvsli | xyv --
	0? ( 3drop ; ) drop nip
	guiact dup >r var2 swap y>scl r> var1 ! ;

:riellv
	ink@ x1 x2 + 2/ dup dup blanco 1- hlinea gris hlinea 1+ negro hlinea ink ;
	
:dvsli
	riellv dup var2 over var1 @ scl>y
	y1 max y2 min dup x2 x1 + 2/ swap x2 x1 - 2/ 2/ dup >r fcircle r>
	2/ dup >r - x2 x1 + 2/ r - swap r> 2/ blanco fcircle ;
	
::.vsli | max var --  | slide de 0..max
	'cvsli 'dvsli .ug2 ;
	
|--- boton tabla
|#acel 0
#tx #ty #tx2 #ty2
#sx #sy #ix #iy

:defbox | f c --
	y2 x2 y1 x1 'tx !+ !+ !+ ! 		| guardo cuadro
:defadj
	y2 y1 - swap / 'sy ! x2 x1 - swap / 'sx ! 
	x1 'ix ! y1 'iy ! ;

:undefbox
	ty2 tx2 ty tx 'x1 !+ !+ !+ ! ;

:cbtabla | xyv --
	0? ( drop guiact var1 dup 16 >> swap $ffff and swap
		over swap defadj
		swap y1 - sy / * swap x1 - sx / + guiact var2 exec ; )
	3drop ;
	
:dbtabla
	dup var3 over var1 dup 16 >> swap $ffff and swap 2dup defbox
	0 >r ( 1? )( 1- over ( 1? )( 1-
			iy ix over sy + over sx + 2swap >win
			pick3 r swap exec 1 r+
			sx 'ix +! ) drop 
		tx 'ix ! sy 'iy +! ) 3drop rdrop 
	undefbox ;

::.btabla | draw click f c --
	16 << or 'cbtabla 'dbtabla .ug3 ;

|--- HTOOL botonera horizontal de estados
:chtool | xyv --
	0? ( 3drop ; ) 2drop
	guiact var1 swap x>scl guiact var2 ! ;

:dhtool | 
	dup var3 >r dup var2 @ 
	over var1 dup hsplit
	swap over swap - 1- swap 
	( 1? )( 1- 
		verde over =? ( 4 claro )( -4 oscuro ) dup dup +dim
		rcaja r@+ blanco rpoli neg dup +dim
		xnext ) 
	2drop rdrop ;
	
::.htool | botonera var cnt --
	'chtool 'dhtool .ug3 ;

|--- TOOLTABLE 
:cbtabla1 | xyv --
	0? ( 3drop ; ) drop
	guiact var1 dup 16 >> swap $ffff and swap
	over swap defadj
	swap y1 - sy / * swap x1 - sx / + guiact var2 exec ; 
	
:dbtabla1
	dup var3 over var1 dup 16 >> swap $ffff and swap 2dup defbox
	0 >r ( 1? )( 1- over ( 1? )( 1-
			iy ix over sy + over sx + 2swap >win
			pick3 r swap exec 1 r+
			sx 'ix +! ) drop 
		tx 'ix ! sy 'iy +! ) 3drop rdrop 
	undefbox ;

::.dtabla | draw click f c --
	16 << or 'cbtabla1 'dbtabla1 .ug3 ;

|--- OPCBTN botonera de estados
:copcbtn | xyv --
	0? ( 3drop ; ) 2drop
	guiact var1 swap x>scl guiact var2 ! ;

:dopcbtn | 
	dup var3 >r dup var2 @ 
	over var1 dup hsplit
	swap over swap - 1- swap 
	( 1? )( 1- 
		verde over =? ( 4 claro )( -4 oscuro ) dup dup +dim
		rcaja r@+ blanco rpoli neg dup +dim
		xnext ) 
	2drop rdrop ;
	
::.opcbtn | botonera var cnt --
	'copcbtn 'dopcbtn .ug3 ;

|=================================================
#lastime 
#startime

::.restart
	lastime 'startime !	;

::.page
	teclados> ukeymap 127 move 512 'teclados> +!
	teclados> keymap 127 move 512 'teclados> +! 
	inikey ;

::.show
	za> dlg> !+ 'dlg> !	zf> 'za> ! zns> 'zf> !
	'msdown start! msec 'lastime ! 0 '.exit ! 0 zns> !
	.restart
	( .exit 0? )( drop update msec 
		lastime >? ( 30 + 'lastime ! r exec dlg.draw redraw )( drop ) 
		) drop 
	0 '.exit ! 0 motion! 0 end! 
	za> dup 'zf> ! 'zns> ! dlg> 4 - dup 'dlg> ! @ 'za> ! 
	0 zns> ! 
	-512 'teclados> +! keymap teclados> 127 move
	-512 'teclados> +! ukeymap teclados> 127 move
	;
	
::.segs | seg --
    1000 * lastime startime - >? ( drop rdrop ; ) drop	;
    
|--- dialogos basicos
#:ifin ( -30 -31 -60 -55 -82 -71 -66 -87 -50 -63 -26 -31 4 -63 36 -87 62 -71 78 -55 94 -31 70 1 36 33 68 57 94 73 78 89 62 65 38 33 4 65 -28 89 -50 73 -66 57 -82 33 -58 1 -28 0 )
#:iok ( -79 -14 -63 -28 -7 36 65 -76 89 -44 17 68 -11 -79 -12 0 )

#dline )( 256

:gdline | "" --
	here >r 'dline 'here ! ,print 0 ,c r> 'here ! ;
	
::dlgprint | "" --
	gdline
	.page
	'exit <=esp
	.show cls
		32 32 screen blanco
		'dline print 	;
		
::dlgsino | "" -- 0/1
	gdline oscurecetodo
	.page
	8 8 .table
	rojo oscuro 2 4 .at 'ifin [ 0 exit ; ] dup =>esc .sbtn
	verde oscuro 5 4 .at 'iok [ 1 exit ; ] dup =>esp .sbtn
	.show 16 16 screen blanco
		3 4 pat 10 8 ptofill
		negro 0 6 pat 'dline pcenter print ;
