| simplest voxel heigthmap
| PHREDA 2017
|--------------------------

^r4/lib/gui.txt
^r4/lib/loadpng.txt
^r4/lib/img.txt

#x 512.0		| x position on the map
#y 800.0		| y position on the map
#angle 0.0		| direction of the camera
#horizon 100	| horizon position (look up and down)
#height 100		| height of the camera

#map
#mapa

#plx #ply
#dx #dy
#invz
#invsw

#hidden )( 8192

#sina
#cosa

:inicam
	angle sincos 'cosa ! 'sina !
	'hidden >r 0 ( sw <? )( sh r!+ 1+ ) drop rdrop ;

:iniray | z
	cosa neg sina -  over *. 'plx !
	cosa sina - over *.
	plx - invsw *. 'dx !

	sina cosa - over *. 'ply !
	sina neg cosa - over *.
	ply - invsw *. 'dy !
	x 'plx +!
	y 'ply +!
	;

:getmap
	ply 6 >> $ffc00 and plx 16 >> $3ff and or 2 << 4+ ;

:vline	| i 'h -- i 'h
	height getmap mapa + @ $ff and - invz *. horizon + clamp0
	over @ >? ( drop 4+ ; )
	dup rot dup @ >r !+ swap
	r> over -	| i 'h he cnt
	pick3 rot setxy		| i 'h cnt
	getmap map + @ swap
	( 1? )( 1- over px!+ sw 1- px+! ) 2drop
	;

:render
	inicam
	1.0	| dz
	5.0 ( 1000.0 <? )(
		iniray
		240.0 over /. 'invz !
		'hidden
		0 ( sw <? )( swap
			vline
			dx 'plx +! dy 'ply +!
			swap 1+ ) 2drop
		swap 0.02 + swap over + ) 2drop ;

:mi
	clrscr verde "cargando..." print redraw
	mark
	"media/img/map1.png" loadpng 'map !
	"media/img/alt1.png" loadpng 'mapa !
	cr "listo..." print redraw
	1.0 sw / 'invsw !
	;

:main
	mi
	show clrscr
		render
		[ 0.01 'angle +! ; ] <le>
		[ -0.01 'angle +! ; ] <ri>
		[ sina 2/ neg 'x +! cosa 2/ neg 'y +! ; ] <spc>

		[ 1 'horizon +! ; ]	<pgup>
		[ -1 'horizon +! ; ] <pgdn>
		[ 1 'height +! ; ] <up>
		[ -1 'height +! ; ] <dn>

		'exit >esc<
		;

: main ;