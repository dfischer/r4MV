| Tanques War
| 2016 - Itinerario
|-----------------
^r4/lib/gui.txt
^r4/lib/part16.txt
^r4/lib/plqlib.txt
^r4/dev/tanque.spr

#tanques 0 0
#misiles 0 0
#piedras 0 0
#fx 0 0

|-----------------
:r00001 rand 0.0001 mod ;
:r0001 rand 0.001 mod ;
:r001 rand 0.01 mod ;
:r01 rand 0.1 mod ;

:fpos
	8 >> sh 2/ + swap 8 >> sw 2/ + swap pos ;

#tano 0 0 tano1 10 tano2 10 tano3 10 0

:secani | 'secani -- 'spr
	>a a@ 0? ( a> 8 + a!+ a!+ a@ ; )
	a> 4+ @ 1+ over 4+ @ | 'spr cnt max
	<? ( a> 4+ ! @ ; ) drop
	8 + dup @ 0? ( 2drop a> 8 + dup @ ) | adr spr
	swap a!+ 0 a! ;


|-------------------------
:tbox | adr --
	dup @ 1- 0? ( nip ; )
	|dup 2* alpha
	swap !+ >b
	b@+ 'tx1 ! -1 b> +! b@+ 'ty1 !
	b@+ 'tx2 ! -1 b> +! b@+ 'ty2 !
	b@+ drop |fillbox
	b@+ dup $ffff and swap 16 >> $ffff and boxpad
	b@+ dup 12 >> $f and swap $fff and setfont
	b@+ b@+ dup ink 24 >> boxprintn
	;


:+tbox | "" w h x y  --
	'tbox 'fx p!+ >a
	64 a!+ | vida
	over pick4 - a!+
	dup pick3 - a!+
	rot + rot rot + a!+ a!+
	0 a!+ | fondo
	0 a!+	| pad
	$1032 a!+ | font size
	a!+
	$a00ff00 a!+
	;

|-----------------
:phumo
	dup @ 1- 0? ( nip ; )
	dup 2* alpha
	swap !+ >b
	b@+	b@+ fpos
	b@+ b> 12 - +!	| VX
	b@+ b> 12 - +!	| VY
	b@+ qdim
	b@+ b@+ rnsprite
	b@+ b> 8 - +!
	255 alpha
	;

:+humo | x y --
	'phumo 'fx p!+ >a
	127 a!+ | vida
	a!+ a!+ | x y
	r0001 a!+ r0001 a!+ | vx vy
	r0001 4 + a!+		| tamaño
	'humo2 a!+			| dibujo
	0 a!+
	r001 a!+
	;

:+estela | x y --
	'phumo 'fx p!+ >a
	64 a!+
	a!+ a!+ | x y
	r0001 a!+ r0001 a!+
	r00001 10 + a!+		| tamaño
	'humo2 a!+			| dibujo
	0 a!+
	r001 a!+
	;

:golpet1
|	0 'tb2 ! 0 'vt !
|	10 'danio +!
|	danio 100 >=? ( ganotanque2 exit ) drop
|	10 ( 1? )( yt xt +humo 1- ) drop
	;

:golpet2
|+	0 'tb ! 0 'vt2 !
|	10 'danio2 +!
|	danio2 100 >=? ( ganotanque1 exit ) drop
|	10 ( 1? )( yt2 xt2 +humo 1- ) drop
	;

:hitmisil | x y tanque -- 0
    "-10" 2over fpos 50 20 xc yc +tbox
	5 ( 1? )( 1-
		pick3 pick3 swap
		+humo )
	4drop 0 ;

:pmisil
	dup @ 1- 0? ( nip ; )	| tiempo de vida
	swap !+ >b
	b@+ b@+
|	rnd $1000 nand? ( pick2 pick2 swap +estela ) drop
	2dup 'tanques 0.05 p.in2d? 1? ( hitmisil ; ) drop
	fpos 48 qdim
	'misil b@+ rnsprite
	b@+ b> 16 - +!
	b@+ b> 16 - +!
	;

:+misil
	dup >a a@+ a@+ a> 4+ @	| x y d
	'pmisil 'misiles p!+ >a
	170 a!+
	rot a!+ swap a!+
	dup a!+
	sincos 0.01 *. a!+ 0.01 *. a!+
	-8 a+
	a@+ 10* a> 16 - +!
	a@+ 10* a> 16 - +!
	;


|-------------------------
:oruga
	over
	1? ( 'tano secani )( 'tano1 )
	nip over rnsprite ;

:drawtan | rot --
	oruga
	'tanc over rnsprite
	'tant over rnsprite
 	;

:drawtan2 | rot --
	oruga
	'tanc2 over rnsprite
	'tant over rnsprite
 	;

:drawtan3 | rot --
	oruga
	'tanc3 over rnsprite
	'tant over rnsprite
 	;


| control de tanque
| rigth
| left
| up
| fire

:tcontrol
|	%1 and? ( -0.001 turn )
|	%10 and? ( 0.001 turn )
|	%100 and? ( 0.001 adv )
|	%1000 and? ( +misil )
	;

| tanque 1
| x y dibujo dt
| (adt) (vt)
| daño energia id
:t1
	>b
	80 qdim
	b@+	b@+ fpos
	b> 8 +
	[ -0.001 over ! ; ] <le>
	[ 0.001 over ! ; ] <ri>
	[ 0 over ! ; ] >le<
	[ 0 over ! ; ] >ri<
	4+
	[ 0.001 over ! ; ] <up>
	[ 0 over ! ; ] >up<
	4 - >b
	b@+ b> 8 - +!
	b> dup @ swap 8 - @ drawtan | vt dt
	sincos pick2 *. b> 20 - +! *. b> 16 - +!
    b>
	20 - '+misil <dn>
	drop
	;

:t2
	>b
	80 qdim
	b@+	b@+ fpos
	b> 8 +
	[ -0.001 over ! ; ] 30 ?key
	[ 0.001 over ! ; ] 32 ?key
	[ 0 over ! ; ] 30 ?ukey
	[ 0 over ! ; ] 32 ?ukey
	4+
	[ 0.001 over ! ; ] 17 ?key
	[ 0 over ! ; ] 17 ?ukey
	4 - >b
	b@+ b> 8 - +!
	b> dup @ swap 8 - @ drawtan2 | vt dt
	sincos pick2 *. b> 20 - +! *. b> 16 - +!
	b> 20 -
	'+misil 31 ?key
	drop
	;

| tanque de prueba robot
:t3
	>b
	80 qdim
	b@+	b@+ fpos
	b> 8 +
	[ -0.001 over ! ; ] 30 ?key
	[ 0.001 over ! ; ] 32 ?key
	[ 0 over ! ; ] 30 ?ukey
	[ 0 over ! ; ] 32 ?ukey
	4+
	[ 0.001 over ! ; ] 17 ?key
	[ 0 over ! ; ] 17 ?ukey
	4 - >b
	b@+ b> 8 - +!
	b> dup @ swap 8 - @ drawtan3 | vt dt
	sincos pick2 *. b> 20 - +! *. b> 16 - +!
	b> 20 -
	'+misil 31 ?key
	drop
	;


:reset
	'tanques p.clear
	'piedras p.clear
	'fx p.clear

	't1 'tanques p!+ >b
	0 0 0 0 'stanque 0 -0.5
	b!+ b!+ b!+ b!+ b!+ b!+
	0 b!+
	100 b!+	| energia
	1 b!+ | id tanque

	't2 'tanques p!+ >b
	0 0 0 0.5 'stanque 0 0.5
	b!+ b!+ b!+ b!+ b!+ b!+
	0 b!+
	100 b!+	| energia
	2 b!+ | id tanque

	't3 'tanques p!+ >b
	0 0 0 0.25 'stanque 0.3 0.5
	b!+ b!+ b!+ b!+ b!+ b!+
	0 b!+
	100 b!+	| energia
	3 b!+ | id tanque

	;

|--------------
:ganotanque2
	show clrscr
		"gano tanque2!" print
		'exit >esc< ;

:ganotanque1
	show clrscr
		"gano tanque1!" print
		'exit >esc< ;


:main
	reset
	33
	show clrscr
		dup "%d" print
		'tanques p.draw
		'fx p.draw
		'misiles p.draw

		'exit >esc<
		cminiflecha ;

:memoria
	mark
	10 'tanques p.ini
	100 'misiles p.ini
	100 'piedras p.ini
	1000 'fx p.ini
	;

: memoria main ;
