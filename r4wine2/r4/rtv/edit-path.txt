| path editor test
|-------------------------------------
^r4/lib/gui.txt
^r4/lib/btn.txt

^r4/lib/vesprite.txt
^r4/rtv/fx1.vsp

^r4/lib/trace.txt

|-----------------
#color $00

#trazo )( $fffff
#trazo> 'trazo

:-trazo
	'trazo 0 over ! 'trazo> ! ;

:++trazo | px --
	trazo> 4 - @ =? ( drop ; )
:+trazo | p --
	trazo> !+ 0 over ! 'trazo> ! ;

:dibujotrazo
	'trazo @+
	0? ( 2drop ; )
	d>xy op
	( @+ 1? )(
		d>xy line ) 2drop
	;


|----- modos de cursor
:dibujando
	[ xymouse xy>d ++trazo ; ]
	[ ; ]
	guiDraw
	dibujotrazo ;

:puntoapunto
	dibujotrazo
	[ xymouse line  ; ]
	[ xymouse xy>d ++trazo ; ]
	guiDraw
	;

|----- animadores
#deltat
#prevt

:itime
	msec 'prevt ! 0 'deltat ! ;

:dtime
	msec dup prevt - 'deltat ! 'prevt ! ;

#tl 4.0
#mt	| escala de tiempo
#tn 0	| tiempo actual

:]tra
	2 << 'trazo + @ ;

:llerp | t a b -- r | a + t * (b - a) | t 0.0 .. 1.0
	over - rot 16 *>> + ;

:lineal | -- x y
	trazo> 'trazo - 2 >> 1-		| cnt
	dup tn mt *. *				| cnt f
	dup $ffff and swap 16 >> 	| cnt f i
	rot over 1+ min				| f i i+1%cnt
	]tra swap ]tra swap
	pick2 pick2 d>y pick2 d>y llerp >r
	swap d>x swap d>x llerp r>
	;


|-----
:catmullRom | p0 p1 p2 p3 t -- v
	>r pick2 - 2/ 	| p0 p1 p2 v1
	>r rot over
	swap - 2/ r>	| p1 p2 v0 v1
	2dup +			| p1 p2 v0 v1 v0+v1
	pick3 2* - pick4 2* +
	r dup dup *. *. *.	| p1 p2 v0 v1 A
	pick4 dup 2* + neg pick4 dup 2* + + pick3 2* - rot -
	r dup *. *. +		| p1 p2 v0 A+B
	swap r> *. +
	nip + ;

#x2 #y2
#x3 #y3
#x4 #y4
:catrom | -- x y
	trazo> 'trazo - 2 >> 1-		| cnt
	dup tn mt *. *				| cnt f
	dup $ffff and swap 16 >> 	| cnt f i
	rot >r
	dup 1- r max		| f i i-1
	swap dup 1+ r min	| f i-1 i i+1
	dup 1+ r> min		| f i-1 i i+1 i+2
	]tra d>xy 'y4 ! 'x4 !
	]tra d>xy 'y3 ! 'x3 !
	]tra d>xy 'y2 ! 'x2 !
	]tra d>xy rot swap y2 y3 y4 pick4 catmullRom >r
	x2 x3 rot x4 swap catmullRom r>
	;


:drawsp
	pos
	64 qdim
	'svg6 vesprite ;

:followpathl
	0 'tn !
	1.0 tl 1000 *. /. 'mt !
	itime
	show clrscr
		|--- vista
		verde
		dtime
		deltat 'tn +!
		tn "%d " print
		tn mt *.
		1.0 >? ( 0 'tn ! )
		"%f " print
		lineal drawsp
		'exit >esc<
		cminiflecha ;

:followpathc
	0 'tn !
	1.0 tl 1000 *. /. 'mt !
	itime
	show clrscr
		|--- vista
		verde
		dtime
		deltat 'tn +!
		tn "%d " print
		tn mt *.
		1.0 >? ( 0 'tn ! )
		"%f " print
		catrom drawsp
		'exit >esc<
		cminiflecha ;

:edpath  | --
	-trazo
	33
	show clrscr
		|--- vista
		verde
		dup "%d " print
|		dibujando
		puntoapunto

		'followpathl <f2>
		'followpathc <f3>
		'exit >esc<
		cminiflecha ;


|--------------------------------------------------
:	mark
	edpath
	;
