^r4/lib/gui.txt
^r4/lib/part16.txt
^r4/lib/plqlib.txt

^r4/lib/loadjpg.txt
^r4/lib/loadpng.txt
^r4/lib/loadbmp.txt

#archivo

#tanim 0
#ttime 2000
#fondo 0
#cfondo $ffffff
#tabs )( 64
#ctab )( 64
#sizefnt
#typefnt
#colfnt
#xxc #yyc

#placas )( 64
#placas> 'placas
#nplaca 'placas

#placast )( 64
#placanow 'placast

#pantalla )( $ffff
#pantalla> 'pantalla

#fx 0 0
#objs 0 0

|--------------------------
#xcam #ycam #zcam 2.0
#rxcam #rycam #rzcam

|--------------------------
:,placa | tt --
	placas> 'placas - 'placast + !
	pantalla> placas> !+ 'placas> !
	;

:,p | val --
	pantalla> !+ 'pantalla> ! ;

:resetplaca
|	'objs p.clear
|	'fx p.clear

	'placas 'placas> !
	'pantalla 'pantalla> !
	0 'fondo !
	$ffffff 'cfondo !
	0 'colfnt !
	;

|---------------------------------------
:startpant
	-1.6 'xxc !
	-1.0 'yyc !
	11 'typefnt !
	0.15 'sizefnt !
	0.15 1 'tabs !+ !
	typefnt sizefnt setfont3d
	;

:>>l | adr -- adr'
	( c@+ $ff and 31 >? )( drop )
	0? ( drop 1- ; ) drop
	0 over 1- c!
	;

#sumx

:boxl | "" tab -- "" x
	sumx swap 'sumx +! ;
:boxc | "" tab -- "" x
	swap sizeprint 2/ rot
	dup 2/ sumx + swap 'sumx +!
	swap - ;
:boxr | "" tab -- "" x
	swap sizeprint rot
	dup sumx + swap 'sumx +!
	swap - ;

#llbox boxl boxc boxr

:,tbox | "" tipo tab --
	pick2 c@ 0? ( 4drop ; ) drop

	colfnt 8 << ,p
	swap 1- 2 << 'llbox + @ exec
	,p yyc ,p
	typefnt ,p sizefnt ,p
	,p
	0 ,p
	0 ,p
	;


:espag	|@@
	( c@+ $ff and 31 >? )(
		$61 =? ( swap str>nro 'tanim ! swap ) | a
		$74 =? ( swap getfenro 1000 *. 'ttime ! swap ) | t
		drop
		) drop
|	>>cr
    ttime tanim 24 << or ,placa
	startpant ;

:eslin 	|--
	$01 colfnt 8 << or ,p
	xxc ,p yyc ,p
	xxc 3.2 + ,p yyc 0.01 + ,p
	0 ,p 0 ,p 0 ,p
	0.1 'yyc +!
	>>cr ;

:>>| | adr -- adr'
	( c@+ $ff and 31 >? )(
		$7c =? ( ; )
		drop ) ;

:esTexto
	'ctab >r
	dup r!+
	( >>| $7c =? )( drop
		0 over 1- c!
		dup r!+
		) drop
	0 over 1- c!
	r> 'ctab
	'tabs >r
	xxc 'sumx !
	( over <? )(
		@+
		r@+ r@+ ,tbox
		) 2drop
	rdrop
	sizefnt 2/ 'yyc +!
	;

|-------- configuracion
:gett | adr -- adr' valor
	c@+
	$6c =? ( drop str>nro 1 ; ) | l
	$63 =? ( drop str>nro 2 ; ) | c
	$72 =? ( drop str>nro 3 ; ) | r
	drop 1- 0 ;

:ctabs
	'tabs >r
	( gett 1? )(
		r!+ 3.2 * 100 / r!+	| 3.2 ancho pantalla
		dup c@ $2c =? ( drop 1+ )( drop ) | ,
		) drop
	rdrop
	1- >>cr
	;

:cfnts
	str>nro 'typefnt !
	1+ getfenro 'sizefnt !
	1+ str$>nro 'colfnt !
	typefnt sizefnt setfont3d
	1- >>cr
	;

:es;
	c@+
	$7c =? ( drop ctabs ; ) | ;|l10,c20,r20
	$66 =? ( drop cfnts ; )	| ;f11,0.15,ffffff |fuente,size,color

	drop
	1- >>cr ;

|--------------------
:setimg | adr -- adr'
	dup 'fondo ! >>l ;

:setfon | adr -- adr'
	str$>nro 'cfondo ! >>cr ;

| iatv/coso.p.jpg	| imagen de fondo
| fff0000			| color de fondo
:parseprev | adr -- adr'
	0 'fondo !
	( c@+ 1? )(
    	$40 =? ( drop 1- ; )
    	$69 =? ( drop setimg 0 ) | i
    	$66 =? ( drop setfon 0 ) | f
    	drop
		)
	drop 1- ;
	;
|----------------------------
| @@ cambio de pantalla t1.5 a0
| -- linea separadora 	c00ff00
| ;		config
|----------------------------
:parsec | adr c -- adr
	$3b =? ( drop es; ; ) | ; configura

	$40 =? ( drop dup c@ $40 =? ( drop espag ; ) ) |@@
	$2d =? ( drop dup c@ $2d =? ( drop eslin ; ) ) |--
    drop 1-
	esTexto
    ;

:quita10 | mem -- endmem
	dup
	( c@+ 1? )(
		13 =? ( over c@ 10 =? ( drop swap 1+ swap )( drop ) )
		10 =? ( drop c@+ 13 <>? ( drop 1- 13 ) )
		rot c!+ swap )
	nip swap c!+ ;

:loadimg | filename -- img
	".jpg" =pos 1? ( drop loadjpg ; ) drop
	".png" =pos 1? ( drop loadpng ; ) drop
	".bmp" =pos 1? ( drop loadbmp ; )
	2drop 0 ;

|-------------------------------------------
:xprints | "" --
	ccx ccy ink@
	0 ink
	0.005 dup +atxy
	pick3 ( c@+ 1? )( emitf ) 2drop
	ink atxy
	( c@+ 1? )( emitf ) 2drop
	;

:xprintn | "" --
	( c@+ 1? )( emitf ) 2drop ;

:to3d 0 project3d ;

:fillbox3d | x y x2 y2 --
	2over to3d op
	pick3 over to3d pline
	2dup to3d pline
	drop over to3d pline
	to3d pline
	poli ;

:xbox | x1 y1 x2 y2 --
	2over to3d 2dup >r >r op
	pick3 over to3d pline
	over swap to3d pline
	swap to3d pline drop
	r> r> pline
	gris poli ;

:xprintb | "" --
	ink@ >r
	sizeprint
	ccx ccy rot pick2 + cch pick2 + xbox
	r> ink
	( c@+ 1? )( emitf ) 2drop ;

|-----------------------------
:otexto
	>r r@+
	mpush
	r@+ r@+ setfont3d
	r@+ r@+ r@+ mtransi
	r@+ 1? ( mrotxi )( drop )
	r@+ 1? ( mrotyi )( drop )
	r@+ 1? ( mrotzi )( drop )
	r@+ ink
	r@+ r@+ atxy
	xprintn
	rdrop
	mpop ;

#pw
:+texto | color x y font size "" --
	'otexto 'objs p!+ >r
	pick2 pick2 swap setfont3d
	sizeprint 'pw !
	r!+				| ""
	r!+	r!+			| font size
	pw 2/ + r!+ r!+ 0 r!+	| x y z
	0 r!+ 0 r!+ 0 r!+		| rx ry rz
	r!+						| ink
	pw 2/ neg r!+
	cch neg 2/ r!+          | atxy
	rdrop ;


|--- caja
:obox
	>r r@+ r@+ r@+ r@+
	r@+ ink
    fillbox3d
    rdrop ;

:+box	| color x1 y1 x2 y3 --
	'obox 'objs p!+ >r
	r!+ r!+ r!+ r!+ r!+
	rdrop ;

|------ inicia objetos de animacion
| texto -- color x y fnt size "texto"
:t0	8 >>
	swap >r
	r@+ r@+ swap
	r@+ r@+ swap
	r> @ +texto ;

| caja -- color x y x y
:t1 8 >>
	swap >r
	r@+ r@+ swap
	r@+ r@+ swap
	+box
	rdrop ;

:t2
:t3
:t4
:t5
:t6
:t7
:t8 2drop ;

#lidraw t0 t1 t2 t3 t4 t5 t6 t7 t8 t8 t8 t8 t8 t8 t8 t8

:inidrawbox
	@+ dup $f and 2 << 'lidraw + @ exec ;

:inidrawpant | adr --
	'objs p.clear
|	+logo
	@+ swap @ swap
	( over <? )(
		dup inidrawbox
		32 + ) 2drop ;

|------------------------------------------------



::loadplaca | "" --
	here dup 'archivo !
	swap load 0 swap !
	archivo quita10
	'here !
	archivo
	parseprev
	startpant
	( c@+ 1? )( parsec )
	2drop
	0 ,placa


	fondo 0? ( drop ; ) loadimg 'fondo !
	;

::preplaca
	cfondo paper
	fondo 1? ( 0 0 sw sh fondo img.drawsize )( cls )
	drop
	>xfb
	'placas 'nplaca !
	'placast 'placanow !

	nplaca inidrawpant
	;

:updplaca
	omode
|	rxcam 1? ( mrotxi )( drop )
|	rycam 1? ( mrotyi )( drop )
|	rzcam 1? ( mrotzi )( drop )
	xcam ycam zcam mtrans
|	nplaca drawpant
|	dtime
	'fx p.draw
	'objs p.draw
	;

:main
	1024 'objs p.ini
	1024 'fx p.ini

   	"atv/urgente.txt" loadplaca
   	preplaca
   	show xfb>scr

    	updplaca
		reloj
   		'exit >esc<
   		cminiflecha
   	;

: mark main ;