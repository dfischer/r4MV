| Volcan polícromo
| Adaptacion del programa SimpleParticle (Yabasic). Galileo (2016)

^r4/lib/gui.txt

|numero de particulas, posicion de origen, aceleracion, tempo
#nParts 10000 #xo 500 #yo 400 #aa 10.0 #t 0.05

| #vx #vy #xp #yp #colorin | campos del registro
| 0   4   8   12  16
#Parts | )( 200000 | 1000 * 20

:int 16 >> ;

:ccolor | b g r --
  8 << or 8 << or ;

:ran | n -- n
  rnd swap mod abs ;

:dot | c x y
|  0 <? ( 3drop ; ) sh >=? ( 3drop ; )	| no hace falta..
|  swap 0 <? ( 3drop ; ) sw >=? ( 3drop ; ) swap
  setxy a!+ ;

:nuevapart | adr -- adr++
  >a
  40 ran 20 - 16 << a!+
  80 ran neg 16 << a!+
  xo 16 << a!+
  yo 16 << a!+
  rnd a!+
  a> ;

| #vx #vy #xp #yp #colorin | campos del registro
| 0   4   8   12  16
:getxy | adr -- x y
	8 + @+ int swap @ int ;

:+vel! | adr --
	@+ swap 4+ swap t *. swap +! ;

:evaluaydibuja | adr -- adr++
	$0 over getxy dot | borra pixel de antigua posicion
	dup +vel! 			| suma pos x
	dup 4+ +vel! 		| suma pos y
	aa t *. over 4+ +! 	| gravedad
	dup getxy sh >=? ( 2drop nuevaPart ; )
	pick2 16 + @ rot rot dot | dibuja punto
	20 +
	;

:volcan
	Parts nParts ( 1? )( swap
		evaluaydibuja
		swap 1- ) 2drop ;

:inicializa
	mark
	here 'Parts !
	Parts nParts ( 1? )( swap
		nuevaPart
		swap 1- ) 2drop ;

: msec rndseed inicializa $000000 'paper ! cls show volcan 'exit >esc< ;