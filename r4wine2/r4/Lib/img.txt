| IMG lib
| PHREDA 2015
|------------------------
| format
| xy|type|img..
^r4/lib/math.txt

#wb #hb		| ancho alto
#paleta

|-- internas para clip
#xi #yi
#wi #hi

|-- internas para scale
#wr #hr
#sx #sy
#xa #ya

::img.wh | 'img -- w h
	@ dup $ffff and swap 16 >> $ffff and ;

|----- 1:1
:clip | x y adr -- adr x y
	rot | y adr x
	wb over + sw >? ( sw pick2 - )( wb ) 'wi ! drop
	-? ( dup 'wi +! neg 2 << 'xi ! 0 )( 0 'xi ! )
	rot | adr x y
	hb over + sh >? ( sh pick2 - )( hb ) 'hi ! drop
	-? ( dup 'hi +! neg wb * 2 << 'xi +! 0 )
	;

|---- opaque
:draw0 | adr -- ;32 bit/pixel
	xi + >b
	wb wi - 2 <<
   	sw wi - 2 <<
	hi ( 1? )(
		wi ( 1? )( b@+ a!+ 1- ) drop
		over a+
		pick2 b+
		1- ) 3drop ;

#odraw draw0 0 0 0 0 0 0 0

::img.draw | x y 'img  --
	0? ( 3drop ; )
	@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	clip | adr x y
	wi hi or -? ( 4drop ; ) drop
	setxy
	@+ $10 and? ( over 'paleta ! dup 8 >> $ff and 2 << rot + swap )
	$7 and 2 << 'odraw + @ exec ;

|---- tile draw
:tdrawline0 | adr --
	wr ( 1? )(
		over >b
		wb <? ( dup )( wb )
		dup ( 1? )( b@+ a!+ 1- ) drop
		- ) 2drop ;

:tdraw0 | adr -- ;32 bit/pixel
   	sw wr - 2 <<
	hr ( 1? )(
		pick2 >b
		hb <? ( dup )( hb )
		dup ( 1? )( b> tdrawline0 1- pick3 a+ wb 2 << b+ ) drop
		- ) 3drop ;

#tdraw tdraw0 0 0 0 0 0 0 0

::img.drawtile | x y w h 'img --
	0? ( 4drop drop ; ) >b
	b@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	'hr ! 'wr !
	setxy
	b> @+ $10 and? ( over 'paleta ! dup 8 >> $ff and 2 << rot + swap )
	$7 and 2 << 'tdraw + @ exec ;


|----- N:N
:clipsc | x y adr -- adr x y
	rot | y adr x
	wr over + sw >? ( sw pick2 - )( wr ) 'wi ! drop
	-? ( dup 'wi +! neg 'xi ! 0 )( 0 'xi ! )
	rot | adr x y
	hr over + sh >? ( sh pick2 - )( hr ) 'hi ! drop
	-? ( dup 'hi +! neg 'yi ! 0 )( 0 'yi ! )
	wb wr 16 <</ dup 'sx ! xi * 'xi !
	hb hr 16 <</ dup 'sy ! yi * 'yi !
	;

:sdraw0 | x y adr --
	yi 'ya !
	sw wi - 2 << | columna
	hi ( 1? )(
		xi 'xa !
	 	pick2 ya 16 >> wb * 2 << +
		wi ( 1? )(
			over xa 16 >> 2 << + @ a!+
			sx 'xa +!
			1- ) 2drop
		over a+
		sy 'ya +!
		1- )
	3drop ;

#sdraw sdraw0 0 0 0 0 0 0 0

::img.drawsize | x y w h 'img --
	0? ( 4drop drop ; ) >b
	b@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	0? ( 4drop ; ) 'hr !
	0? ( 3drop ; ) 'wr !
	b> clipsc
	wi hi or -? ( 4drop ; ) drop
	setxy
	@+ $10 and? ( over 'paleta ! dup 8 >> $ff and 2 << rot + swap )
	$7 and 2 << 'sdraw + @ exec ;

::img.drawscale | x y scale 'img --
	0? ( 4drop ; ) >b
	b@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	dup hb *. 0? ( 4drop ; ) 'hr !
	wb *. 0? ( 3drop ; ) 'wr !
	b> clipsc
	wi hi or -? ( 4drop ; ) drop
	setxy
	@+ $10 and? ( over 'paleta ! dup 8 >> $ff and 2 << rot + swap )
	$7 and 2 << 'sdraw + @ exec ;

|---- w/alpha
:acpx!+ | col --
|	$ff000000 nand? ( drop 1 px+! ; )
	$ff over 24 >> $ff and -
	0? ( drop a!+ ; )
	$ff =? ( 2drop 4 a+ ; )
	swap
	dup $ff00ff and				| alpha color colorand
	a@ dup $ff00ff and 		| alpha color colorand inkc inkcand
	pick2 - pick4 8 *>> rot +	| alpha color inkc inkcandl
	$ff00ff and >r				| alpha color inkc
	swap $ff00 and 				| alpha px colorand
	swap $ff00 and 				| alpha colorand pxa
	over - rot 8 *>> + $ff00 and
	r> or a!+  ;

:adraw0 | adr -- ;32 bit/pixel
	xi + >b
	wb wi - 2 <<
   	sw wi - 2 <<
	hi ( 1? )(
		wi ( 1? )( b@+ acpx!+ 1- ) drop
		over a+
		pick2 b+
		1- ) 3drop ;

#aodraw adraw0 0 0 0 0 0 0 0

::img.adraw | x y 'img --
	0? ( 3drop ; )
	@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	clip | adr x y
	wi hi or -? ( 4drop ; ) drop
	setxy
	@+ $10 and? ( over 'paleta ! dup 8 >> $ff and 2 << rot + swap )
	$7 and 2 << 'aodraw + @ exec ;


:asdraw0
	yi 'ya !
	sw wi - 2 << | columna
	hi ( 1? )(
		xi 'xa !
	 	pick2 ya 16 >> wb * 2 << +
		wi ( 1? )(
			over xa 16 >> 2 << + @ acpx!+
			sx 'xa +!
			1- ) 2drop
		over a+
		sy 'ya +!
		1- )
	3drop ;

#asdraw asdraw0 0 0 0 0 0 0 0

::img.adrawsize | x y w h 'img --
	0? ( 4drop drop ; ) >b
	b@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	0? ( 4drop ; ) 'hr !
	0? ( 3drop ; ) 'wr !
	b> clipsc
	wi hi or -? ( 4drop ; ) drop
	setxy
	@+ $10 and? ( over 'paleta ! dup 8 >> $ff and 2 << rot + swap )
	$7 and 2 << 'asdraw + @ exec ;

::img.adrawscale | x y scale 'img --
	0? ( 4drop ; ) >b
	b@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	dup hb *. 0? ( 4drop ; ) 'hr !
	wb *. 0? ( 3drop ; ) 'wr !
	b> clipsc
	wi hi or -? ( 4drop ; ) drop
	setxy
	@+ $10 and? ( over 'paleta ! dup 8 >> $ff and 2 << rot + swap )
	$7 and 2 << 'asdraw + @ exec ;

::img.get | x y 'img -- col
	0? ( 3drop ; ) >b
	b@+ dup $ffff and 'wb ! 16 >> $ffff and 'hb !
	wb * + 2 << b> + @ ;