|-----------------------------------------------------------
| lista de sprites
| c/sprite tiene
| x y s r 'adr
|-----------------------------------------------------------

| x y r 'adr
|-----------------------------------------------------------
::spos! | x y 'adr --
	!+ ! ;
::smove! | x y 'adr --
	>b swap b@ + b!+ b@ + b! ;
::srot! | a 'adr --
	16 + >b b@ + b! ;
::sscale! | z 'adr --
	8 + >b dup b@ + b!+ b@ + b! ;
::sdib! | 'n 'adr --
	20 + ! ;

:hit? | s1 s2 -- 0/s1
	over =? ( 2drop 0 ; ) | s1=s2?
	>b
	dup @+ b@+ -	| s1 s1y dx
	swap @ b@+ -	| s1 dx dy
	distfast			| s1 dist
	over 8 + @ b@+ + | s1 dist dw
	pick2 12 + @ b@ +
	distfast 2/ -		| s1 dd
	+? ( 2drop 0 ; )
	drop ;

::sHitLayer | 'spr 'adr --  0/'otro
	( @+ 1? )( pick2  | 'spr 'adr 'nesp 'spr
		hit? 1? ( nip nip ; )
		drop ) 3drop 0 ;

::sMapLayer | 'func 'layer --
	( @+ 1? )( pick2 exec ) 3drop ;

::sDropLayer | 'spr 'layer --
	( @+ 0? ( 3drop ; ) pick2 <>? )( drop ) drop nip
	( dup @ 1? )( swap 4 - !+ 4+ ) swap 4 - !
	;

::sAddLayer | 'sprit 'layer --
	( @+ 1? )( drop )
	rot rot 4 - !+ ! ;
