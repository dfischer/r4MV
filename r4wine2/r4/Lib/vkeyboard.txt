^r4/lib/btn.txt

#teclasAlpha1
"Qq1" "Ww2" "Ee3" "Rr4" "Tt5" "Yy6" "Uu7" "Ii8" "Oo9" "Pp0"
"Aa!" "Ss@" "Dd#" "Ff$" "Gg""" "Hh&" "Jj*" "Kk?" "Ll/" "^_\"
"Zz_" "Xx%" "Cc'" "Vv(" "Bb)" "Nn-" "Mm+" "[{." "|=;" "]},"
",;<" "   " ".:>" "   " "   " "   " "AAA" "123" ">!" "<--"

#modo 0
#:vkey 0

:kbac	1 'vkey ! ;
:kdel   2 'vkey ! ;
:kins   3 'vkey ! ;
:kenter 13 'vkey ! ;
:ktab   9 'vkey ! ;
:kcambio	modo 1 xor 1 and 'modo ! ;
:kcambion   2 'modo ! ;

#especial kbac kdel kins kcambion kcambio ktab kenter

:tecesp
	dup 2 << 'especial + @ exec ;

#icons "BAC" "DEL" "INS" "123" "aBc" "->|" "<--"

:teclaesp
	33 -
	home azul
	'tecesp btn
	blanco 2 << 'icons + printc ;

:tec
	dup 2 << 'teclasAlpha1 + modo + c@ 'vkey ! ;

:tecla
	32 >? ( teclaesp ; )
	home negro
	'tec btn
	blanco 2 << 'teclasAlpha1 +
	modo + c@ "%k" printc ;

#bw #bh
::vkeyboard
	sw 10 / sh 12 /	2dup dim
	'bh ! 'bw !
	0
	0 ( 4 <? )(
		0 ( 10 <? )(
			bw over * bw 2/ +
			bh pick3 * bh 2/ + bh 3 << +
			pos
			rot dup tecla
			1+ rot rot
			1+ ) drop
		1+ ) 2drop ;
