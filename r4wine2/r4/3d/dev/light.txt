| iluminacion 2d
| PHREDA 2016
|-------------------
^r4/lib/gui.txt
^r4/lib/img.txt
^r4/lib/loadpng.txt


#ima #ina
#ima0 #ina0
#ima1 #ina1
#dx #dy #dz

:convert | y x normal color -- ligth
	>r >r
	xymouse
	pick3 - 'dy !
	over - 'dx !
	100 'dz !
	r>
	dup 16 >> $ff and dx 8 *>>
	over 8 >> $ff and dy 8 *>> +
	swap $ff and dz 8 *>> +
	$ff clamp0max
	r>
	dup $ff and pick2 8 *>>
	over $ff00 and pick3 8 *>> $ff00 and or
	swap $ff0000 and rot 8 *>> $ff0000 and or
	;

:drawlight | x y --
	ima 4+ >r
	0 0 setxy
	256 ( 1? )( 1-
		256 ( 1? )( 1-
			r ina ima - + @	| normal
			r@+			| color
			convert
			px!+
		) drop
		sw 256 - px+!
	) drop
	rdrop
	;

:filldist
	0 0 setxy
	0 ( sh <? )(
		0 ( sw <? )(
			xymouse 
			pick3 - swap pick2 - swap
			distfast px!+
			1+ ) drop
		1+ ) drop
	;
:testdist
	show clrscr
		filldist
		azul
		sw 2/ sh 2/ op
		xymouse line

		'exit >esc<
		cminiflecha
	;

#nline 0

:dpoint | nro val -- nro
	2dup $ff and 256 + setxy $ff px!+
	2dup 8 >> $ff and 256 + setxy $ff00 px!+
	2dup 16 >> $ff and 256 + setxy $ff0000 px!+
	drop
	;
:dumpline
	verde
	768 256 nline + op
	800 256 nline + line
	ina 4+
	nline 8 << 2 << +
	>r
	0 ( 256 <? )(
     	r@+ dpoint
		1+ ) drop
	rdrop
	;
:main
	show clrscr

		drawlight

		512 0 ima img.draw
		512 256 ina img.draw

		dumpline

		'exit >esc<
		[ ima0 'ima ! ina0 'ina ! ; ] <f1>
		[ ima1 'ima ! ina1 'ina ! ; ] <f2>
		'testdist <f3>
		[ 1 'nline +! ; ] <dn>
		[ -1 'nline +! ; ] <up>

		cminiflecha ;

:ini
	mark
	"media/face.png" loadpng 'ima0 !
	"media/face_n.png" loadpng 'ina0 !
	"media/buddah.png" loadpng 'ima1 !
	"media/buddah_n.png" loadpng 'ina1 !
	ima0 'ima ! ina0 'ina !
	;

: ini main
	;