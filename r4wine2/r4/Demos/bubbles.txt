| bubles
| PHREDA 2012

^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/part16.txt
^r4/demos/simple.spr

#bubles 0 0

:resetgame
	'bubles p.clear
	;

|---------
:r.01 rand 0.001 mod ;
:r.1 rand 1.0 mod ;
:r.8 rand 8.0 mod ;

:bub | adr -- adr/adr 0 delete
 	>b
	mpush
	b@+
	18.0 >? ( 18.0 b> 4 - ! b> 4+ dup @ neg swap ! )
	-18.0 <? ( -18.0 b> 4 - ! b> 4+ dup @ neg swap ! )
	b@+
	-18.0 <? ( -18.0 b> 4 - ! b> 4+ dup @ neg swap ! )
	0 mtransi
	b@+ b> 12 - +!
	b@ 0.01 - b> ! | gravedad
	b@+ b> 12 - +!
	'burbuja 3dnsprite
	mpop
	;

:+buble
	'bub 'bubles p!+ >a
	r.8 r.8 a!+ a!+
	r.1 r.1 a!+ a! ;

:collision | p1 p2 -- p1 p2
	over 4+ @ over 4+ @ - dup *. | (x1-x2)^2
	pick2 8 + @ pick2 8 + @ - dup *. +
	4.0 >=? ( drop ; ) sqrt. 2.0 swap -
	2/ >r
	over 4+ @ over 4+ @ -
	pick2 8 + @ pick2 8 + @ -
	atan2 sincos swap				| p1 p2 si co
	dup r@ *. pick4 4+ +!
	dup r@ *. neg pick3 4+ +!
	over r@ *. pick4 8 + +!
	over r@ *. neg pick3 8 + +!

	dup r@ *. pick4 12 + +!
	dup r@ *. neg pick3 12 + +!
	over r@ *. pick4 16 + +!
	over r@ *. neg pick3 16 + +!

	2drop
	r> drop
	;

:main
	resetgame
	show clrscr
		scr home blanco
		dup "%d " print
		"<SPC> to add bubbles..." print cr cr2
		fonti2
		'+buble dup " + " sp verde btnt sp <spc>
		'exit dup " X " sp rojo btnt sp >esc<
		fonti
		omode
		0 0 -40.0 mtrans
		'bubles p.draw
		'collision 'bubles p.map2
		;

:inicio
	mark
	1000 'bubles p.ini
	;

: inicio 33 main ;

