| Editor de includes en vsprite
| PHREDA 2010
|--------------------------------------------------
^r4/lib/gui.txt
^r4/lib/btn.txt
^r4/lib/input.txt
^r4/lib/trazo.txt

^r4/lib/img.txt
^r4/lib/loadjpg.txt
^r4/lib/loadpng.txt
^r4/lib/loadbmp.txt

^r4/lib/dlgfile.txt
^r4/lib/trace.txt

^r4/lib/tool16.ico


#nombre )( 256

#actual
#cntvar
#indexn )( 3200 | 100 nombres de 32
#index )( $ffff

#cntlist
#inilist
#collist

:viewlist | actual -- actual
	inilist <? ( dup 'inilist ! ; )
	inilist cntlist + >=? ( dup cntlist - 1+ 'inilist ! )
	;

:existname? | "" -- "" 1/0
	'indexn
	cntvar ( 1? )(
		pick2 pick2 =s 1? ( nip nip ; )
		drop swap 32 + swap 1- ) nip ;

:freename | -- ""
	0
	( dup "new%d" mprint existname? 1? )( 2drop 1+ )
	drop nip
	;
|------------------------------
#icohw $f0f
#icomem )( 256

:pl
	ink@
	dup px!+ dup px!+ dup px!+ dup px!+ dup px!+ dup px!+ dup px!+ px!+
	sw 8 - px+! ;
:px8
	pl pl pl pl pl pl pl pl
	sw 3 << neg 8 + px+! ;
:px+
	8 px+! ;
:pxline
	3 << sw 3 << swap - px+! ;

::drawzico
	>r r@+ dup $ff and swap 8 >> $ff and
	( 1? )( r@+
		pick2 ( 1? )( 1- swap
			1 and? ( px8 )( px+ )
			2/ swap ) 2drop
		over pxline
		1- ) 2drop
	rdrop ;

|-------------------------------
:invico
	'icomem >r
	32 ( 1? )( 1- r @ not r!+ ) drop
	rdrop ;

:upico
	icohw 8 >> $ff and 1-
	'icomem >r
	r@+ swap
	( 1? )( 1-
		r @ r 4 - ! 4 r+ ) drop
	r> 4 - ! ;

:dnico
	icohw 8 >> $ff and
 	dup 2 << 'icomem + >r
	1+ ( 1? )( 1-
		r @ r 4 + ! -4 r+
		) drop
	icohw 8 >> $ff and 2 << 'icomem + @ r> 4+ !
	;
:leico
	icohw $ff and 1-
	'icomem >r
	32 ( 1? )( 1- r @
		dup 2/ swap 1 and pick3 << or
		r!+ ) 2drop
	rdrop ;

:riico
	icohw $ff and 1-
	'icomem >r
	32 ( 1? )( 1- r @
		dup 2* swap pick3 >> 1 and or
		r!+ ) 2drop
	rdrop ;

:mirbit | nro nbit -- nroi
	0 swap
	( 1? )(
		pick2 1 and
		rot 2* or swap
		rot 2/ rot rot
		1- ) drop nip ;

:mhico
	'icomem >r
	icohw dup $ff and swap 8 >> $ff and
	( 1? )( r @
		pick2 mirbit
		r!+ 1- )
	2drop rdrop	;

:mvico
	'icomem >r
	0 icohw 8 >> $ff and 1-
	( over >? )(
 	    dup 2 << r + @
 	    pick2 2 << r + @
 	    pick2 2 << r + !
 	    pick2 2 << r + !
		1- swap 1+ swap ) 2drop
	rdrop ;

#aux )( 256

:paso | val --
	'aux 32 ( 1? )( 1-
		rot rot dup @ 2* | i 'a a+
		pick2 1 and or swap !+
		swap 2/ swap
		rot ) 3drop ;

:vhico
	'aux 32 ( 1? )( 1- 0 rot !+ swap ) 2drop
	'icomem >r
	icohw 8 >> $ff and
	( 1? )( r@+ paso 1- ) drop
	rdrop
	'icomem >r
	'aux 32 ( 1? )( 1- swap @+ r!+ swap ) 2drop
	rdrop
	;

:chgm | a1 a2 cnt --
	( 1? )( 1- >r
		over c@ over c@ swap
		rot c!+ rot rot swap c!+
		r> ) 3drop ;

:chg | a1 a2 icnt --
	rot >r
	( 1? )( 1- swap
		r @ over @ r!+ swap !+
		swap ) 2drop rdrop ;

:chgico | i1 i2 --
	over 5 << 'indexn +
	over 5 << 'indexn + 8 chg
	8 << 'index + swap
	8 << 'index + swap 32 chg
	;

:dnlist
	actual cntvar 1- >=? ( drop ; )
	dup 1+ chgico
	1 'actual +! ;

:uplist
	actual 0? ( drop ; )
	dup 1- chgico
	-1 'actual +! ;

|--- parse
:getnro | txt -- txt' 0 / nro
	( dup c@ 33 <? )( 0? ( ; ) drop 1+ ) drop
	dup ?numero 0? ( ; )
	drop rot drop ;

:cpynom | adr en -- adr''
	swap ( c@+ $ff and 32 >? )(
		rot c!+ swap )
	drop 0 rot c! ;

:getsize@ 8 >> $ff and ;

:cadadibujo | adr $23 -- adr'
	drop c@+ $3a <>? ( drop ; ) drop | #:
	cntvar 5 << 'indexn + cpynom
	cntvar 8 << 'index + >r
	getnro dup r!+ getsize@
	( 1? )( swap getnro r!+ swap 1- ) drop
	rdrop
	1 'cntvar +! ;

:parsefile | "" --
	here dup rot load 0 swap !+ 'here !
	0 'cntvar !
	( c@+ 1? )(
		$23 =? ( cadadibujo dup )
		drop ) 2drop
	;

:copyico | adr --
	'icohw >r
	@+ dup r!+
	getsize@
	( 1? )( swap @+ r!+ swap 1- )
	2drop
	rdrop
	;

:icocopy | adr --
	>r
	'icohw @+ dup r!+
	getsize@
	( 1? )( swap @+ r!+ swap 1- )
	2drop rdrop
	;

:actual! | nro --
	viewlist
	actual 8 << 'index + icocopy
	dup 'actual !
	8 << 'index + copyico
	refreshfoco ;

|--- write
:,codigo | adr --
	@+ dup " $%h" ,print
	8 >> $ff and
	( 1? )( 1- swap
	   @+ " $%h" ,print swap ) 2drop
	,nl ;

:writefile | "" --
	mark
	"| ICO file" ,s ,nl
	0 ( cntvar <? )(
		'indexn over 5 << + "#:%s" ,print
		dup 8 << 'index + ,codigo
		1+ ) drop
	savemem
	empty ;


|--------------------------------------------------
:newdib
	freename |cntvar "new%d" mprint
	cntvar 5 << 'indexn + strcpy
	cntvar 8 << 'index + >r
	$f0f r!+
	16 ( 1? )( 1- 0 r!+ ) drop rdrop
	1 'cntvar +!
	cntvar 1- actual!
	;

:copydib
	freename |cntvar "new%d" mprint
	cntvar 5 << 'indexn + strcpy
	cntvar 8 << 'index + >r
	actual 8 << 'index + @+ r!+
	32 ( 1? )( 1- swap @+ r!+ swap ) 2drop rdrop
	1 'cntvar +!
	cntvar 1- actual!
	;

:deldib
	cntvar 0? ( drop ; ) drop
	actual 8 << 'index +
	actual 1+ 8 << 'index +
	cntvar actual - 8 << 1+ move
	actual 5 << 'indexn +
	actual 1+ 5 << 'indexn +
	cntvar actual - 5 << cmove
	-1 'cntvar +!
	actual 1- clamp0 'actual ! 
	actual 8 << 'index + copyico
	;

#imagen
#imagenx
#imageny

:loadimg | filename -- img
	".jpg" =pos 1? ( drop loadjpg ; ) drop
	".png" =pos 1? ( drop loadpng ; ) drop
	".bmp" =pos 1? ( drop loadbmp ; )
	2drop 0 ;

:cargaimg
	mark
	"media/img" dlgfileload 0? ( drop empty ; )
	loadimg 0? ( drop empty ; )
	'imagen !
	0 'imagenx !
	0 'imageny !
	empty
	;

#capx 16
#capy 16
#ox
#oy

#marca 0
:marca1
	marca not 'marca ! ;

:getpixel
	over oy + over ox +
	swap setxy
	px@ 7 >>
	1 and over << ;

:makeico
	cntvar
	freename |	dup "new%d" mprint
	over 5 << 'indexn + strcpy
	8 << 'index + >r
	capy 8 << capx or r!+
	0 ( capy <? )(
		0 >r
		0 ( capx <? )(
			getpixel r+
			1+ ) drop
		r> r!+
		1+ ) drop
	rdrop
	1 'cntvar +!
	cntvar 1- actual!
	exit ;

:drawimg
	imagen 0? ( drop ; )
	imagenx imageny 16 + rot img.draw

	'makeico <enter>

	blink 1? ( blanco )( negro ) drop
	ox oy over capx + over capy +
	caja
	;

|----------------------------
#namedraw 0

:editname
	namedraw 0? ( drop ; ) 31 input
	[ 0 'namedraw ! ; ] lostfoco
	;

:rendib
	actual 5 << 'indexn + 'namedraw ! ;

#varx 'ox #vary 'oy
#sumxy 1
:import
	show clrscr

        drawimg

		fonti
		verde oscuro 1 linesfill blanco
		verde dup " :R%d" print
		blanco "iMPORT iCO " print
		capy capx "%dx%d " print

		[ xymouse 'oy ! 'ox ! ; ] guiBtn

		azul
		[ 16 dup 'capx ! 'capy ! ; ] " 16 " link
		[ 24 dup 'capx ! 'capy ! ; ] " 24 " link
		[ 32 dup 'capx ! 'capy ! ; ] " 32 " link
		cyan
		'cargaimg dup <f1> " Load " link
		'marca1 dup <f2> " set " link
		rojo
		'exit dup >esc< "x" link


		[ 'ox 'varx ! 'oy 'vary ! 1 'sumxy ! ; ] >shift<
		[ 'imagenx 'varx ! 'imageny 'vary ! 16 'sumxy ! ; ] <shift>
		[ sumxy neg vary +! ; ] <up>
		[ sumxy vary +! ; ] <dn>
		[ sumxy neg varx +! ; ] <le>
		[ sumxy varx +! ; ] <ri>

		cminiflecha ;


|---------------------------------------------------
:invp | x y --
	2 << 'icomem + >r 1 swap << r @ xor r> ! ;

#xa #ya
:dn
	xymouse 'ya ! 'xa ! ;
:mv
	xa ya xymouse op line ;
:up
	'invp vop!
	xymouse
	48 - 3 >> swap 10 - 3 >> swap vop
	xa 10 - 3 >> ya 48 - 3 >> vline! ;

:editico
	verde
	1 26 gotoxy
	'upico 'i_aup ibtn sp |ibtn " U " btnt sp
	'dnico 'i_adn ibtn sp |" D " btnt sp
	'leico 'i_ale ibtn sp |" L " btnt sp
	'riico 'i_ari ibtn sp |" R " btnt
	'invico 'i_wb ibtn sp |" N " btnt sp
	'mhico 'i_mhor ibtn sp |"MH " btnt sp
	'mvico 'i_mver ibtn sp |"Mv " btnt sp
	'vhico 'iturn ibtn sp |"MVH" btnt sp

   	azul cr cr sp
	'newdib dup <f1> 'i_new ibtn |"f1-New" link
	'copydib dup <f2> 'i_copy sp ibtn |"f2-Copy" link
	'rendib dup <f3> 'i_name sp ibtn |"f3-Rename" link
	'import dup <f4> 'i_img sp ibtn |"f4-IMG" link
	'uplist 'i_sup sp ibtn |" U " btnt sp
	'dnlist 'i_sdn sp ibtn |" D " btnt sp
	'deldib dup <del> 'i_del sp ibtn |"Del" link

	cyan cr cr cr sp
	[ icohw dup $ff and 1- 0 max swap $ff00 and or 'icohw ! ; ] "-" btnt sp
	icohw $ff and "w:%d" print
	[ icohw dup $ff and 1+ 32 min swap $ff00 and or 'icohw ! ; ] "+" sp btnt
	sp sp
	[ icohw dup $ff00 and $100 - 0 max swap $ff and or 'icohw ! ; ] "-" btnt sp
	icohw 8 >> $ff and "h:%d" print
	[ icohw dup $ff00 and $100 + $2000 min swap $ff and or 'icohw ! ; ] "+" sp btnt
	[ $1010 'icohw ! ; ] "16" sp btnt
	[ $1818 'icohw ! ; ] "24" sp btnt
	[ $2020 'icohw ! ; ] "32" sp btnt

	gris oscuro
	10 48
	icohw $ff and 3 << pick2 +
	icohw 8 >> $ff and 3 << pick2 +
	gcxyxy gc.fbox

	blanco
	'dn 'mv 'up guiMap
	10 48 setxy
	'icohw drawzico
	;

|----------------------------
:cadadib | n n -- n n
	dup inilist + cntvar >=? ( drop ; )
	actual =? ( verde oscuro )( gris oscuro )
	[ dup actual! ; ]
	200 32 btnfpx
	blanco
	dup 8 << 'index + sp drawico
	5 << 'indexn + " #%s" print
	cr cr cr ;

:tabladib
	36 3 gotoxy chome!
	0 ( cntlist <? )(
		cadadib 1+ ) drop ;

:viewall
	scr 62 3 gotoxy chome!
	0
	( collist ( 1? )( 1-
		over 8 << 'index + drawico
		swap 1+
		cntvar >=? ( 2drop ; )
		swap )
	drop cr cr ) ;

:kdn
	actual cntvar 1- >=? ( drop ; )
	1+ actual! ;

:kup
	actual 0? ( drop ; )
	1- actual! ;

:main
	'nombre "mem/inc-ico.mem" load drop
	'nombre parsefile
	actual 8 << 'index + copyico
	fonti scr home
	rows 3 / 1- 'cntlist !
	sw 64 - 5 >> 1- 'collist !
	0 'inilist !
	show clrscr
		verde oscuro 2 linesfill blanco
		rojo
		0 2 atxy
		sp 'exit dup >esc< 'i_exit ibtn |"esc-Exit" link
		sp verde

		verde dup " :R%d" print
		blanco "eDIT iCO" print
		'nombre "%s " printr

        sp editname
		cr
		cr
	| acciones

		'kdn <dn>
		'kup <up>
		scr
        editico
		tabladib
		viewall

		cminiflecha ;

|--------------------------------------------------
:	4 0 paper
	mark
	main
	'nombre writefile
	;

